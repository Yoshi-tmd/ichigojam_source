[ジャンプアクションゲーム]
//from kidspod

20 @F:CLS:CLT 
30 Z=0:X=2:Y=19:C=#FB:J=0:O=13:Q=10:E=#ED 
40 FORI=0TO31:FORJ=20TO5STEP-5:LCI,J:?CHR$(#2):NEXT:NEXT:LC12,20:?"  ":LC22,10:?"  " 
50 M=27:N=19:GSB@R:N=9:GSB@R:M=4:N=14:GSB@L 
70 LC1,4:?"GOAL<" 
90 @C:Z=Z+1:IF Z%2=1:LCO,4:?" ":LCQ+5,14:?" ":LCO+6,9:?" ":LCQ,19:?" ":LCQ+7,4:?" " 
100 IF Z>24 Z=0:ELSE IF Z<13 O=O-Z%2:Q=Q+Z%2:ELSE O=O+Z%2:Q=Q-Z%2 
110 LCX,Y:?CHR$(C)::LCO,4:?CHR$(E):LCQ+5,14:?CHR$(E):LCO+6,9:?CHR$(E):LCQ+7,4:?CHR$(E) 
120 IFSCR(X,Y)=E GOTO@END 
130 IFX=5&&Y=4 LC10,12:?"CLEAR":END 
140 K=INKEY():CLK 
150 IF J=0 C=#F9:ELSE C=#FA 
160 IF K=28&&X>0 IF SCR(X-1,Y)!=#02 LCX,Y:?" ":X=X-1:C=#FD 
170 IF K=29&&X<31 IF SCR(X+1,Y)!=#02 LCX,Y:?" ":X=X+1:C=#FB 
180 IF J>2||J=0 IF SCR(X,Y+1)!=#02 LCX,Y:?" ":Y=Y+1:ELSE J=0 
190 IF J!=0 J=J+1:LCX,Y:?" ":IF J<5&&SCR(X,Y-1)!=#2 Y=Y-1 
200 IF J=0&&K=30 J=1 
210 IF Y=22 GOTO@END 
220 GOTO @C
230 @R:LCM+1,N-4:?"   ":FORI=0TO4:LCM+I,N-I:?CHR$(#2):NEXT:RTN 
240 @L:LCM-3,N-4:?"   ":FORI=0TO4:LCM-I,N-I:?CHR$(#2):NEXT:RTN 
250 @END:LC10,12:?"GAME OVER":END