//へびからいちごを守れ！
from kidspod

// 変数
A   いちごのよこ座標
B   へびのよこ座標
C   へびのよこ座標の変化量
    クリアごとに3足される
E   ゲームの回数
F   ゲームの速さ（へびの動く速さ）
T   へびが動き出すまでの時間

// ゲームソース
10 CLV:F=3
20 A=31-C:B=C:T=RND(50):E=E+1
30 CLS:?"LEVEL";E
40 LC A,12:?CHR$(255)
50 LC 15,13:?CHR$(249)
60 T=T+1:IF T>60 B=B+1:BEEP
70 LC B-1,12:?CHR$(3,60):WAIT F
80 IF B==A GOTO 160
90 IF INKEY()==0 GOTO 60
100 LC 15,12:?CHR$(242)
110 IF 14<B AND B<18 GOTO 120 ELSE GOTO 160 
120 LC 11,6:?"GOOD":PLAY "L16FA":WAIT 60 
125 IF E<15 GOTO 130 ELSE GOTO 180
130 IF E%5==0 C=0:F=F-1 ELSE C=C+3
140 GOTO 20
160 LC 11,16:?"GAMEOVER":PLAY "L8FEC":WAIT 60:CLK
170 IF INKEY()==0 GOTO 170 ELSE GOTO 10
180 LC 11,16:?"GAME CLEAR!!"
190 PLAY"$T1200 O3CDEFGAB O4CDEFGAB O5C":WAIT 175:PLAY

========================================
ステップゲーム作り

・ゲームの設定の確認
    キャラの表示
        プレイヤー
        へび
        イチゴ
    テキストの表示
        スコア
        レベル
    スタートしたら、へびがいちごをめがけて動いていく
    イチゴにたどいて食べる前にキーをおしてとめる
    1回クリアするごとにレベルアップする
        距離が短くなる
----------------------------------------
1.初期化とキャラ、イチゴ、へびの表示
    変数の初期化
    座標系
    文字コード

10 CLV
20 A=31-C:B=C
30 CLS
40 LC A,12:?CHR$(255)
50 LC 15,13:?CHR$(249)
70 LC B-1,12:?CHR$(3,60)
RUN
----------------------------------------
2.ヘビをうごかす
    座標の変化
    くり返し
    音の出力

60  B=B+1:BEEP
90 GOTO 60
RUN
----------------------------------------
3.ヘビのスピードの遅くする
    待機命令
    変数

10 CLV:F=3
70 LC B-1,12:?CHR$(3,60):WAIT F
RUN
----------------------------------------
4.ヘビがいちごを食べたらGAMEOVER
　GAMEOVERになったら、キーをおして再スタート
    GOTOコマンド
    比較分岐

80 IF B==A GOTO 160
160 LC 11,16:?"GAMEOVER":PLAY "L8FEC":WAIT 60:CLK
170 IF INKEY()==0 GOTO 170 ELSE GOTO 10
RUN
----------------------------------------
5.キーを押してヘビを止める
    INKEY()関数

90 IF INKEY()==0 GOTO 60
100 LC 15,12:?CHR$(242)
RUN
----------------------------------------
6.タイミングの判定、ゲームのくり返し
    比較分岐
    論理積

110 IF 14<B AND B<18 GOTO 120 ELSE GOTO 160 
120 LC 11,6:?"GOOD":PLAY "L16FA":WAIT 60 
150 GOTO 20
RUN
----------------------------------------
7.ヘビが動き出すタイミングをランダムにする

20 A=31-C:B=C:T=RND(50)
60 T=T+1:IF T>60 B=B+1:BEEP
RUN
----------------------------------------
8.レベルの設定
　クリアするごとに、距離が近くなり、5回に1回にスピードが早くなる
　
20 A=31-C:B=C:T=RND(5):E=E+1
30 CLS:?"LEVEL";E
140 IF E%5==0 C=0:F=F-1 ELSE C=C+3
RUN
----------------------------------------
9.15回クリアすると、ゲームクリア

130 IF E<15 GOTO 130 ELSE GOTO 180
180 LC 11,16:?"GAME CLEAR!!"
190 PLAY"$T1200 O3CDEFGAB O4CDEFGAB O5C":WAIT 175:PLAY
RUN
