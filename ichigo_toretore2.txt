# いちごとれとれ2ゲーム
//ランダムな場所にいちごが出現。キャラを動かしてイチゴを時間内にたくさんとる
//制限時間の初期値は20秒。

ーーーーーーーーーーーーーーーーーーーーーーーーーーーーーーー

##　Tips
#   キャラの移動
#   当たり判定

ーーーーーーーーーーーーーーーーーーーーーーーーーーーーーーー

10 CLS:CLT:X=15:Y=12:S=0
20 C=0
30 IF C%5==0 LC RND(32),RND(23):?CHR$(255)
40 C=C+1
50 LC X,Y:?" "
60 K=INKEY()
70 IF K==28 X=X-1
80 IF K==29 X=X+1
90 IF K==30 Y=Y-1
100 IF K==31 Y=Y+1
105 IF SCR(X,Y)==255 S=S+1
110 LC X,Y:?"@"
120 LC 0,0:?S
130 IF TICK()>1200 END
200 GOTO 30

ーーーーーーーーーーーーーーーーーーーーーーーーーーーーーーー
[STEPゲームづくり]
1.キャラを表示して、動かす
    10 CLS:X=15:Y=12
    20 LC X,Y:?" "
    30 K=INKEY()
    40 IF K==28 X=X-1
    50 IF K==29 X=X+1
    60 IF K==30 Y=Y-1
    70 IF K==31 Y=Y+1
    80 LC X,Y:?"@"
    90 GOTO 20

2.イチゴ畑を表示しよう
//alt+V
    15 LC 17,12:?"ÿ  ÿ  ÿ  ÿ  ÿ"

3.イチゴを撮った数を表示する
    10 CLS:X=15:Y=12:S=0
    85 LC 0,0:?S

4．イチゴをとった数を増やす
    今のままでは数が増えない
    イチゴを取った数をふやすには、足し算をする
        S=S+1
    これで、イチゴを1個とったら1増やせるようになった
    この式を、プログラムに入れてみよう

        75 S=S+1

    勝手に増えちゃうね
    コンピュータは、書いてあるコマンドや計算を勝手に実行しちゃう。（順番通りに、素直に）
    だから、ある時はこのコマンドを実行するけど、そうじゃない時はこのコマンドを実行しないように、
    コンピュータがコマンドを実行するかしないかを選べるようにする仕組みがある
    それが分岐処理です。

    今のゲームで、ある時、っていうのは、イチゴをとった時、のこと。
    だから、こう考えよう。
    「もし、イチゴをとったら、イチゴの数を1足すようにする」
        （そうじゃない時は足し算をしない）
    これをプログラムに書くと、こんなかんじになります。
    イチゴは、1個目の場所を指しながら、このイチゴの場所の、横と縦の数字は何かな？
        横17、縦12
    イチゴを取ったという時は、キャラクターとイチゴが同じ場所に出ている時
    キャラとイチゴの横と縦の数が同じになった時は、こう書く
        (X==17)AND(Y==12)
    プログラムにするとこうなる。

    75 IF (X==17)AND(Y==12) S=S+1
    
    IFが、分岐処理をしますよって、コンピュータに教えるコマンド
    IFコマンドがあると、コンピュータは分岐があるなとわかる
    その後に、どんな時に分岐するかを決めることが書いてあるので、それをチェックする
    ここに書いてあることがその時に起きているなら、次のコマンドを実行する
        起きていなかったら、実行しない

5.これだと、変なことがおきる
    これだと、場所だけで分岐しちゃう
    イチゴだったらというように変える

        75 IF SCR(X,Y)==ASC("ÿ") S=S+1

6.イチゴを5個取ったら終わりにする
    88 IF S==5 END

7.完成
    簡単なイチゴ取りゲームの完成

8.もっとイチゴを出して、時間内にどれだけ取れるかゲームに改造
9.イチゴをランダムに出現させる
    10 CLS:X=15:Y=12:S=0:C=0
    15 IF C%5==0 LC RND(32),RND(23):?CHR$(255)
    17 C=C+1
    90 GOTO 15

    //5個取ったら終わるコードが残っているの
10.時間制限にする
    制限時間は20秒  //何秒でもOK
    10 CLS:X=15:Y=12:S=0:C=0:CLT
    88 IF TICK()>1200 END

11.改造
    制限時間を変える
    キャラ、イチゴをかえる
    範囲の制限をつける
