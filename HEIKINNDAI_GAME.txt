10 'HEIKINNDAI GAME
20 CLT:X=5:Y=5 
30 CLS
40 LC 5,5:?CHR$(1,0,1,1,1,0,1,1,1,0,1)
50 FOR I=0 TO 3
60 LC 5,I+6:?CHR$(1,0,1,0,1,0,1,0,1,0,1)
70 NEXT
80 LC 5,I+6:?CHR$(1,1,1,0,1,1,1,0,1,1,1)
90 A=X:B=Y
100 IF X==15 AND Y==5 LC 0,0:?"GOAL! ";TICK()/60;" SEC":END
110 K=INKEY()
120 IF K==28 X=X-1
130 IF K==29 X=X+1
140 IF K==30 Y=Y-1
150 IF K==31 Y=Y+1
160 IF SCR(X,Y)==0 ?"X":END
162 LC A,B:?CHR$(1)
165 LC X,Y:?CHR$(250)
170 WAIT 4:GOTO 90

==========================================================
==========================================================

#チラツキがひどいので、作り直し

10 'HEIKINNDAI GAME
20 CLT:X=5:Y=5 
30 CLS:WAIT 1
40 LC 5,5:?CHR$(1,0,1,1,1,0,1,1,1,0,1)
50 FOR I=0 TO 3
60 LC 5,I+6:?CHR$(1,0,1,0,1,0,1,0,1,0,1)
70 NEXT
80 LC 5,I+6:?CHR$(1,1,1,0,1,1,1,0,1,1,1)
90 LC X,Y:?CHR$(250)
100 IF X==15 AND Y==5 LC 0,0:?"GOAL! ";TICK()/60;" SEC":END
110 K=INKEY()
120 IF K==28 X=X-1
130 IF K==29 X=X+1
140 IF K==30 Y=Y-1
150 IF K==31 Y=Y+1
160 IF SCR(X,Y)==0 ?"X":END
170 GOTO 30

==========================================================

1.タイトルをつける。平均台を作る。
10 'HEIKINDAI GAME
30 CLS
40 LC 5,5:?CHR$(1,0,1,1,1,0,1,1,1,0,1)
50 FOR I=0 TO 3
60 LC 5,I+6:?CHR$(1,0,1,0,1,0,1,0,1,0,1)
70 NEXT
80 LC 5,I+6:?CHR$(1,1,1,0,1,1,1,0,1,1,1)
——————————————————
2.キャラクターを出す
90 LC X,Y:?CHR$(250)
——————————————————
3.人を動かす
110 K=INKEY()
120 IF K==28 X=X-1
130 IF K==29 X=X+1
140 IF K==30 Y=Y-1
150 IF K==31 Y=Y+1
170 GOTO 30
——————————————————
4.画面のチラツキを抑える
170 WAIT 2:GOTO 30
——————————————————
5.人を平均台にのせる
20 X=5:Y=5 
——————————————————
6.平均台から落ちたらゲームオーバー
160 IF SCR(X,Y)==0 ?"X":END
——————————————————
7.ゴールの設定を作る
100 IF X==15 AND Y==5 END
——————————————————
8.ゴールしたらタイムが出るようにする
20 CLT:X=5:Y=5
100 IF X==15 AND Y==5 LC 0,0:?"GOAL! ";TICK()/60;" SEC":END
——————————————————
9.完成！