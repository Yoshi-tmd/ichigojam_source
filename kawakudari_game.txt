[かわくだりゲーム]
//ノーマル
10 CLS:CLT:X=15
20 LC X,5:?"0"
30 LC RND(32),23:?"*"
35 WAIT 2
36 X=X-BTN(28)+BTN(29)
38 IF SCR(X,5) ?TICK():END
40 GOTO 20


//二人対戦
*38と39のWINをとれば協力プレイ
10 CLS:X=10:Y=20:CLT
20 LC X,5:?"0"
25 LC Y,5:?"T"
30 LC RND(32),23:?"*"
35 WAIT 2:K=INKEY()
36 X=X-(K==28)+(K==29)
37 Y=Y-(K==90)+(K==67)
38 IF SCR(X,5) ?TICK();"  T WIN!":END
39 IF SCR(Y,5) ?TICK();"  0 WIN!":END
40 GOTO 20


//まんてんの復習用改造
10 CLS:CLT:X=15:S=0
20 LC X,5:?CHR$(251)
25 IF RND(5)>0 C=42 ELSE C=255
30 LC RND(32),23:?CHR$(C)
40 WAIT 2
50 X=X-BTN(28)+BTN(29)
55 X=(X+32)%32
57 IF SCR(X,5)==255 GSB 200:GOTO 70
60 IF SCR(X,5) ?TICK():?S:END
70 GOTO 20
200 S=S+1
210 BEEP 10,15
220 RTN

ーーーーーーーーーーーーーーー
STEPコーディング
1.ノーマルかわくだりをSTEPで作る
通常のかわくだりゲームをSTEP方式で作る
解説点
・コマンド、コマンド名、役割
・逐次処理、分岐処理、ループ処理
・言葉の統一
　はこｰ>変数
　みちｰ>逐次処理
　まつｰ>待機命令、WAITコマンド
　？ｰ>プリントコマンド
　わかれみちｰ>分岐処理
　くらべるｰ>条件式、比較演算子
　あんごうｰ>文字コード
　おえかきｰ>座標
　つたえるｰ>入力・出力

	10 CLS:CLT:X=15
	20 LOCATE X,5:PRINT ”ひと”
	30 LOCATE RND(32),23:PRINT ”＊”
	40 WAIT 2
	50 X=X-BTN(28)+BTN(29)
	60 IF SCR(X,5) PRINT TICK():END
	70 GOTO 20

	＞人はAlt+R（chr-> 251）
	＞＊（chr-> 42）
	
	＞作業1
	*PRINTコマンドとLOCATEコマンドを省略形にする
	PRINT　=>　？
	LOCATE　=>　LC
	（LETはそのまま）
	＞作業2
	*キャラをCHR$コマンドで出すようにする
	*絵文字を出すときの文字コードを、ASCコマンドを使って調べる
	　→Alt+Keyで出す絵文字の文字コードを、PRINT ASC(“◯”)を使って調べる
	＞作業3
	*裏技防止
	55 X=(X+32)%32
	
	*簡単なフローチャートを書く
	初期化ｰ>キャラの配置ｰ>キー操作ｰ>当たり判定ｰ>ループ処理

2.改造する
完成形を見せる
改造目標：イチゴをたまに出して、とったら得点を出すゲームに改造

3.イチゴを表示する
イチゴを出てくるようにする
障害物を出しているところを探してもらって、30行目と同じように作ってみる
	35 LC RND(32),23:?CHR$(255)

4.イチゴをたまに出すようにする
挙動が変、障害物がたくさん出るので、たまにイチゴを出すようにする

RNDコマンドで確率的に障害物を出すようにする
文字コードを変数にして、変数に入る数は、分岐処理で決める
25行目の条件式の部分が確率（確率　=　おみくじ、ルーレット）
1，2，3，4はTRUEのコマンド（C=42）を実行、0はFALSEのコマンド（C=255）を実行
	25 IF RND(5)>0 C=42 ELSE C=255
	30 LC RND(32),23:?CHR$(C)
	35は削除

5.イチゴに当たったら音をだす
音をだすのはBEEP　高さ、長さ
分岐処理を使う
	57 IF SCR(X,5)==255 BEEP10,15

6.イチゴにあたっても止まっちゃうバグ
なぜかを考えてみる　=>　記入
	57 IF SCR(X,5)==255 BEEP10,15:GOTO 70

7.得点が入るようにしよう
得点=>数=>イチゴを取ると増える=>変数
変数なので、まずは準備してあげる
ループ処理の外、初期化のプログラムのとこに書こう
	10 CLS:CLT:X=15:S=0

8.得点が増えるようにする
変数の値を増やす
1つずつ増やすのでインクリメント
	57 IF SCR(X,5)==255 S=S+1:BEEP10,15:GOTO 70

9.最後に得点を表示する
	60 IF SCR(X,5) ?TICK():?S:END

10.この前習ったよこみちを作ってみよう
単語について
よこみちｰ>サブプロシージャ
大通りｰ>メインプロシージャ、メインルーチン
	57 IF SCR(X,5)==255 GSB 200:GOTO 70
	200 S=S+1
	210 BEEP 10,15
	220 RTN

11.完成！
自由に改造してね
・イチゴを10とったら特別メッセージが出る　とか
ーーーーーーーーーーーーーーー