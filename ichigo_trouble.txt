// イチゴトラブル
// 電子工作マガジン 2018秋号より

10 CLV:CLS:CLP:A=7:N=16:B=N:X=15:Y=13
20 FOR I=0 TO N:[I*3]=1:[I*3+1]=RND(20)+6:[I*3+2]=RND(10)+5:NEXT
50 LC A,0:?CHR$(255);
60 LC X,Y:?CHR$(249);
70 FOR I=0 TO N:IF ![I*3] GOTO 120
80 LC [I*3+1],[I*3+2]:?" ";
90 [I*3+1]=([I*3+1]+RND(2))%30
100 [I*3+2]=([I*3+2]+RND(2))%22
110 LC [I*3+1],[I*3+2]:?CHR$(237);
120 NEXT
130 LC 0,0:?S;
140 F=X:G=Y
150 X=X-(BTN(28)*(X>0))+(BTN(29)*(X<30))
160 Y=Y-(BTN(30)*(Y>10))+(BTN(31)*(Y<22))
190 LC F,G:?" ";
200 IF !BTN(32) GOTO 50
210 IF X<A Z=A:A=X:X=Z
220 FOR J=A TO X:LC J,0:?CHR$(255);:LC J,Y:?CHR$(255);:NEXT
230 FOR J=0 TO Y:LC A,J:?CHR$(255);:LC X,J:?CHR$(255);:NEXT
240 M=(768-(X-A)*Y)/4:C=1
250 FOR I=0 TO N:IF ![I*3] GOTO 280
260 D=[I*3+1]:E=[I*3+2]
270 IF (D>=A)*(D<=X)*(E<=Y) [I*3]=0:LC D,E:?C;:S=S+M*C:B=B-1:C=C+1:WAIT 15
280 NEXT
290 IF B>-1 A=F:X=F:CLS:GOTO 50
300 LC 9,12:?"SCORE=";S