//ネコから逃げろゲーム（かんたん）
// 2020/01/28   ネコキャッチゲームに変更
//              詳しくは /Users/tomodayoshinori/Documents/myworkspace/IchigoJam/ソースコード/_印刷用/ネコキャッチゲーム.xlsx           

10 CLS:X=15:Y=15:C=1:D=1
20 A=RND(31):B=RND(23)
30 LC A,B:?" "; 
40 A=A+C:B=B+D
50 IF (A<0)+(A>30) C=-1*C
60 IF (B<0)+(B>22) D=-1*D
70 LC A,B:?"ì"; 
80 LC X,Y:?" ";
90 K=INKEY()
100 IF K==28 IF X>0 X=X-1
110 IF K==29 IF X<31 X=X+1
120 IF K==30 IF Y>0 Y=Y-1
130 IF K==31 IF Y<22 Y=Y+1
140 LC X,Y:?"ú";
150 IF (X==A)*(Y==B) END
200 GOTO 30

// ネコを増やす
10 CLS:X=15:Y=15:C=1:D=1:G=1:H=-1
20 A=RND(31):Y=RND(23):E=RND(31):F=RND(23)
72 LC E,F:?" "; 
73 E=E+G:F=F+H
74 IF (E<0)+(E>30) G=-1*G
75 IF (F<0)+(F>22) H=-1*H
76 LC E,F:?"ì"; 
160 IF (X==E)*(Y==F) END

// 2匹とったらクリアする
10 CLS:X=15:Y=15:C=1:D=1::CLT:G=1:H=-1:S=0
20 A=RND(31):B=RND(23):E=RND(31):F=RND(23)
30 LC A,B:?" "; 
50 IF (A<0)+(A>30) C=-1*C
60 IF (B<0)+(B>22) D=-1*D
70 IF A<>99 A=A+C:B=B+D:LC A,B:?"ì"; 
72 LC E,F:?" "; 
74 IF (E<0)+(E>30) G=-1*G
75 IF (F<0)+(F>22) H=-1*H
76 IF E<>99 E=E+G:F=F+H:LC E,F:?"ì"; 
80 LC X,Y:?" ";
90 K=INKEY()
100 IF K==28 IF X>0 X=X-1
110 IF K==29 IF X<31 X=X+1
120 IF K==30 IF Y>0 Y=Y-1
130 IF K==31 IF Y<22 Y=Y+1
140 LC X,Y:?"ú";
145 IF TICK()>900 LC 0,0:?"GAME OVER":END
150 IF (X==A)*(Y==B) S=S+1:A=99
160 IF (X==E)*(Y==F) S=S+1:E=99
170 IF S==2 GOTO 300
200 GOTO 30
300 LC 0,0:?"GAME CLEAR"
310 WAIT 90:GOTO 10

===================================
// ステップ作製

1.画面の初期化とキャラクタの表示
10 CLS:X=15:Y=15
140 LC X,Y:?"ú";
    変数の役割
    X:キャラクタのよこ座標
    Y:キャラクタのたて座標

2.キャラクタを動かす
80 LC X,Y:?" ";
90 K=INKEY()
100 IF K==28 X=X-1
110 IF K==29 X=X+1
120 IF K==30 Y=Y-1
130 IF K==31 Y=Y+1
200 GOTO 80

3.キャラクタに移動の制限をつける
100 IF K==28 IF X>0 X=X-1
110 IF K==29 IF X<31 X=X+1
120 IF K==30 IF Y>0 Y=Y-1
130 IF K==31 IF Y<22 Y=Y+1

4.ランダムな場所にネコを表示
20 A=RND(31):B=RND(23)
70 LC A,B:?"ì"; 
    変数の役割
    A:ネコのよこ座標
    B:ネコのたて座標

5.ネコを動かす
10 CLS:X=15:Y=15:C=1:D=1
30 LC A,B:?" "; 
40 A=A+C:B=B+D
200 GOTO 30
    変数の役割
    C:ネコのよこ方向の速さ
    D:ネコのたて方向の速さ

6.ネコをはね返す
50 IF (A<0)+(A>30) C=-1*C
60 IF (B<0)+(B>22) D=-1*D

7.キャラクタとネコの当たり判定
150 IF (X==A)*(Y==B) END



