# イチゴ避けろゲーム
ーーーーーーーーーーーーーーーーーーーーーーーーーーーーーーー

10 A=0:B=12:S=0
20 C=RND(25)+5
30 CLS
40 ?CHR$(255);S
50 LC A,B:?CHR$(236)
60 LC C,12:?CHR$(255)
65 IF S==5 GOTO 150
70 A=A+1
80 IF A>30 A=0:C=RND(25)+5
90 IF B==9 B=B+3
100 IF INKEY()==32 B=B-3
110 IF SCR(A,B)==255 END
115 IF A==C AND B==9 S=S+1
120 WAIT 5:GOTO 30
150 LC 0,1:?"GAME CLEAR!":END

ーーーーーーーーーーーーーーーーーーーーーーーーーーーーーーー

[STEPコーディング]

1.変数Aの値に1を足す処理をくり返す(くり返しの学習)
10 A=0:B=12
15 ?A
70 A=A+1
120 GOTO 15

* ———
2.速さを調節（WAITコマンド）
120 WAIT 5:GOTO 15


* ———
3.キャラクターを表示して、みぎへ増やす(LCコマンドとCHR$コマンドの使い方)
50 LC A,B:?CHR$(236)

* ———
4.キャラクターを動かすアニメにする（CLSコマンド、パラパラ漫画のようになる）
30 CLS

* ———
5.キャラクターが右端にきたら、左端にワープする（座標、数値の変化）
80 IF A>30 A=0

* ———
6.変数Aの値を表示しないようにする、120のGOTOの引数を変更（行番号の削除、GOTOコマンドの行き先を変更）
15
120 WAIT 5:GOTO 30

* ———
7.キャラクターをジャンプする①（縦座標の数を減らす）
100 IF INKEY()==32 B=B-3

* ———
8.キャラクターをジャンプする②（キャラクターを元の高さに戻す処理）
90 IF B==9 B=B+3

* ———
9.イチゴ（障害物）を表示する（RNDコマンドで場所をランダムにする）
20 C=RND(25)+5
60 LC C,12:?CHR$(255)

* ———
10.当たり判定（SCRコマンド、座標、文字コード、ENDコマンド）
110 IF SCR(A,B)==255 END

* ———
11.ジャンプの判定と得点の表示と追加処理（ANDの使い方、変数のインクリメント、10に追記）
10 A=0:B=12:S=0
40 ?CHR$(255);S
115 IF A==C AND B==9 S=S+1

* ———
12.イチゴの場所を変える（80に追記）
80 IF A>30 A=0:GOTO 20

* ———
13.5回成功したらゲームクリア（GOTOコマンドのワープ、ENDコマンド）
65 IF S==5 GOTO 150
150 LC 0,1:?"GAME CLEAR!":END

ーーーーーーーーーーーーーーーーーーーーーーーーーーーーーーー

[改造ポイント]
■キャラクターの変更
	行番号50のCHR$コマンドの引数を変える

■障害物の変更
	行番号60のCHR$コマンドの引数を変える
	行番号40，110も変える

■速さの調節
	行番号120のWAITコマンドの引数を変える

■ジャンプをIchigoJamボタンにする
	行番号100のINKEY()==32を、BTN()==1に変える

■クリアの規定数を変更
	行番号65のS==5の数を変更

■障害物を上にも追加
	25 D=RND(25)+5
	62 LC D,9:?CHR$(255)
	に改造