
# 障害物避けろゲーム
ーーーーーーーーーーーーーーーーーーーーーーーーーーーーーーー

10 A=0:B=12:S=0:X=1
20 C=RND(20)+5:D=9+RND(2)*3
25 E=RND(25)+5
30 CLS
35 ?CHR$(255);S
40 IF BTN(32) B=9 ELSE B=12
50 A=A+1
60 IF A>31 A=0:GOTO 20
70 LC C,D:?CHR$(X,X,X)
75 LC E,9:?CHR$(255)
80 LC A,B:?CHR$(251)
90 IF S==5 GOTO 130
110 IF SCR(A+1,B)==X ?"OUT!":END
115 IF A==E AND B==9 S=S+1
120 WAIT 2:GOTO 30
130 LC 0,1:?"GAME CLEAR!":END


ーーーーーーーーーーーーーーーーーーーーーーーーーーーーーーー

[STEPコーディング]

1.変数Aの値に1を足す処理をくり返す(くり返しの学習)
10 A=0
50 ?A:A=A+1
120 WAIT 6:GOTO 50

* ———
2.キャラクターを表示して、みぎへ動かす7
10 A=0:B=12
80 LC A,B:?CHR$(251)


* ———
3.キャラクターを動かすアニメにする（CLSコマンド、パラパラ漫画のようになる）
30 CLS
120 WAIT 3:GOTO 30

* ———
4.キャラクターが右端にきたら、左端にワープする（座標、数値の変化）
60 IF A>31 A=0

* ———
5.数字を消す（?A:をトル）
50 A=A+1

* ———
6.キャラクターを動かす（縦座標の数を変える）
40 IF BTN(32) B=9 ELSE B=12

* ———
7.障害物を表示する（RNDコマンドで場所をランダムにする）
10 A=0:B=12:S=0:X=1
20 C=RND(25)+5:D=9+RND(2)*3
70 LC C,D:?CHR$(X,X,X)

* ———
8.当たり判定（SCRコマンド、座標、文字コード、ENDコマンド）
110 IF SCR(A+1,B)==X ?"OUT!":END

* ———
9.イチゴを表示する（RNDコマンドで場所をランダムにする）
25 E=RND(25)+5
75 LC E,9:?CHR$(255)

* ———
10.イチゴを取ったら加点
10 A=0:B=12:S=0
35 ?CHR$(255);S
115 IF A==E AND B==9 S=S+1

* ———
11.イチゴの場所を変える（60に追記）
60 IF A>30 A=0:GOTO 20

* ———
12.5回成功したらゲームクリア（GOTOコマンドのワープ、ENDコマンド）
90 IF S==5 GOTO 130
130 LC 0,1:?"GAME CLEAR!":END

ーーーーーーーーーーーーーーーーーーーーーーーーーーーーーーー

[改造ポイント]
■キャラクターの変更
	行番号80のCHR$コマンドの引数を変える

■障害物の変更
	行番号10のXの数を変える

■速さの調節
	行番号120のWAITコマンドの引数を変える

■クリアの規定数を変更
	行番号90のS==5の数を変更

